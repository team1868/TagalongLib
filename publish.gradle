apply plugin: 'maven-publish'

ext.licenseFile = files("$rootDir/LICENSE.txt")

def pubVersion = '2025.0.0043'

def outputsFolder = file("$buildDir/outputs")

def versionFile = file("$outputsFolder/version.txt")

task outputVersions() {
    description = 'Prints the versions of wpilib to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

task libraryBuild() {}

build.dependsOn outputVersions

task copyAllOutputs(type: Copy) {
    destinationDir file("$buildDir/allOutputs")
    from versionFile
    dependsOn outputVersions
}

build.dependsOn copyAllOutputs
copyAllOutputs.dependsOn outputVersions

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archiveFile
    copyAllOutputs.from task.archiveFile
}

def artifactGroupId = 'com.tagalong.lib'
def baseArtifactId = 'tagalonglib'
def zipBaseName = "_GROUP_tagalong_ID_${baseArtifactId}-cpp_CLS"
def javaBaseName = "_GROUP_tagalong_ID_${baseArtifactId}-java_CLS"

// task cppHeadersZip(type: Zip) {
//     destinationDirectory = outputsFolder
//     archiveBaseName = zipBaseName
//     archiveClassifier = "headers"
//
//     from(licenseFile) {
//         into '/'
//     }
//
//     from('src/main/native/include') {
//         into '/'
//     }
// }

// build.dependsOn cppHeadersZip
// addTaskToCopyAllOutputs(cppHeadersZip)
// build.dependsOn cppSourceZip
// addTaskToCopyAllOutputs(cppSourceZip)

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task outputJar(type: Jar, dependsOn: classes) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    from sourceSets.main.output
}

task outputSourcesJar(type: Jar, dependsOn: classes) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task outputJavadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives outputJar
    archives outputSourcesJar
    archives outputJavadocJar
}

addTaskToCopyAllOutputs(outputSourcesJar)
addTaskToCopyAllOutputs(outputJavadocJar)
addTaskToCopyAllOutputs(outputJar)

build.dependsOn outputSourcesJar
build.dependsOn outputJavadocJar
build.dependsOn outputJar

libraryBuild.dependsOn build

def releasesRepoUrl = "$buildDir/repos/releases"

task cleanReleaseRepo(type: Delete) {
    delete releasesRepoUrl
}

tasks.matching {it != cleanReleaseRepo}.all {it.dependsOn cleanReleaseRepo}

model {
    publishing {
        def taskList = createComponentZipTasks($.components, ['TagalongLib'], zipBaseName, Zip, project, includeStandardZipFormat)
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/team1868/TagalongLib")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GIT_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GIT_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                groupId artifactGroupId
                version pubVersion
                artifactId = "${baseArtifactId}-java"

                artifact jar
                artifact sourcesJar
                artifact javadocJar
                
                description = "test"
                pom.packaging = "jar"
            }
        }
    }
}